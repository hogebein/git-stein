plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'com.github.ben-manes.versions' version '0.39.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'


dependencies {
    api 'org.eclipse.jgit:org.eclipse.jgit:5.12.0.202106070339-r'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'ch.qos.logback:logback-classic:1.2.5'
    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'info.picocli:picocli:4.6.1'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'junit:junit:4.13.2'

    def SQLiteJDBC = 'org.xerial:sqlite-jdbc:3.34.0'
    def ORMLiteJDBC = 'com.j256.ormlite:ormlite-jdbc:5.5'
    if (project.findProperty("stein.usesqlite") == "yes") {
        implementation SQLiteJDBC
        implementation ORMLiteJDBC
    } else {
        compileOnly SQLiteJDBC
        compileOnly ORMLiteJDBC
    }

    // dependencies for sample code: available only if the property is defined
    if (project.findProperty("stein.runsample") == "yes") {
        implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.26.0'
        implementation 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
        implementation 'org.jgrapht:jgrapht-io:1.5.1'
    } else {
        compileOnly 'org.eclipse.jdt:org.eclipse.jdt.core:3.26.0'
        compileOnly 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'
        compileOnly 'org.jgrapht:jgrapht-io:1.5.1'
    }
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes "Main-Class": "jp.ac.titech.c.se.stein.Loader"
    }
}

shadowJar {
  minimize()
}
